apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystores.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    signingConfigs {
        signing {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "com.lacker.visitors"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", "\"http://lacker.backend.debug/fulladdress/\""
            debuggable true
            applicationIdSuffix ".debug"
        }

        //only release version allows google sign in
        release {
            buildConfigField "String", "SERVER_URL", "\"http://lacker.backend.prod/fulladdress/\""
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.signing
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':utils')

    //Basic dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation "com.google.android.material:material:$materialVersion"

    //Flux
    implementation 'com.gitlab.voodoo-rocks:flux-android:1.0.1'

    //Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //Cicerone
    implementation 'ru.terrakok.cicerone:cicerone:5.1.0'

    //Moshi
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitMoshiVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    //DI
    implementation 'com.google.dagger:dagger:2.29.1'
    kapt 'com.google.dagger:dagger-compiler:2.29.1'

    //Time and etc from Java 8
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$coreLibraryDesugaringVersion"

    //Process safe shared prefs
    implementation "com.github.yandextaxitech:binaryprefs:1.0.1"

    //Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    //Http interceptor for testers
    debugImplementation "com.github.chuckerteam.chucker:library:$chuckVersion"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$chuckVersion"

    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

}