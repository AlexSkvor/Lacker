apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystores.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    signingConfigs {
        signing {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.lacker.visitors"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        resValue "string", "client_id", properties.getProperty("client.id", "")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", "\"https://lacker.ru/\""
            debuggable true
            signingConfig signingConfigs.signing
        }

        //only release version allows google sign in
        release {
            buildConfigField "String", "SERVER_URL", "\"https://lacker.ru/\""
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.signing
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    androidExtensions {
        experimental true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':utils')
    implementation project(':styles')
    implementation project(':dto')

    //Basic dependencies
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    implementation "com.google.android.material:material:$materialVersion"

    // Pagination
    implementation "com.gitlab.voodoo-rocks:PaginationView:$paginationVersion"

    //QR
    implementation("com.gitlab.voodoo-rocks:QRCodeReaderView:$customQRVersion")
    implementation("com.google.zxing:core:$zxingVersion") // do not update, later version causes errors

    // Permissions
    implementation("com.github.florent37:runtime-permission-kotlin:$runtimePermissionsHelperVersion")

    //Dimens
    implementation "com.intuit.sdp:sdp-android:$dimensHelperVersion"
    implementation "com.intuit.ssp:ssp-android:$dimensHelperVersion"

    //Flux
    implementation "com.gitlab.voodoo-rocks:flux-android:$fluxVersion"

    //Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //Cicerone
    implementation "ru.terrakok.cicerone:cicerone:$ciceroneVersion"

    //Moshi
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitMoshiVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    //DI
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    //Time and etc from Java 8
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$coreLibraryDesugaringVersion"

    //Process safe shared prefs
    implementation "com.github.yandextaxitech:binaryprefs:$binaryPrefsVersion"

    // Recycler
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-layoutcontainer:$delegateRecyclerVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefreshVersion"

    //Circel Image View
    implementation "de.hdodenhof:circleimageview:$circleImageViewVersion"

    //Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    //Http interceptor for testers
    debugImplementation "com.github.chuckerteam.chucker:library:$chuckVersion"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$chuckVersion"

    implementation "com.google.android.gms:play-services-auth:19.0.0"

    //Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

}